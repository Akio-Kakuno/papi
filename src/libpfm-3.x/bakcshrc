#################################################################
#
#         .cshrc file
#
#	This file contains commands to occur every time
#	a new C-shell is generated. (tcsh too)
#	It is read prior to the .login file at each login.
#
#################################################################

#         set up search path
set path = (/usr/bin \
            /bin \
            /usr/ccs/bin \
            /opt/SUNWspro/bin \
            /usr/lang/bin \
            /usr/local/gnu/bin \
            /usr/local/bin \
            /usr/local/sfw/bin \
            /usr/local/mh \
            /usr/local/mh/bin \
            /usr/openwin/bin \
            /usr/X11R6/bin \
            /usr/local/X11R6.3/bin \
            /usr/local/mtools \
            /usr/local/java/bin \
            /usr/local/matlab \
            /usr/local/microsoft/bin \
            /usr/local/real \
            /usr/local/adobe/bin \
            /usr/local/SUNWns6 \
			/usr/local/SUNWspro/bin \
			/usr/local/rational \
			/usr/local/matlab/bin \
			/usr/local/sfw/kde/bin \
			/usr/local/gnome-1.4/bin \
			/usr/local/netscape \
            /usr/ucb \
            /usr/games \
            ~/bin \
            . \
           )


#	Set the values of environment variables.
#	Various system programs use the values of these variables.
#	Set these here (rather than in .login) so that rsh will pick them up.
#		SHELL: which shell I normally use (Bourne, Cshell, or tcsh).
#		PAGER: puts stuff on the screen a page at a time.
#		EDITOR:  what editor I normally use.
#		PATH:  directories to look in for commands I use. [set above]
#		PRINTER: what printer I normally use.
#               MANPATH: where to go hunting for your man pages (used to add
#                        to the default.

setenv SHELL /bin/csh 			# or /bin/sh or /usr/local/bin/tcsh
setenv PAGER more
setenv EDITOR vi
#setenv PRINTER cslab
set hostname = `uname -n`

if ($hostname =~ cetus[123456][abcdef]) then
    setenv PRINTER cl103
else
    setenv PRINTER cl105
endif  

setenv MORE '-c -s -d'
setenv LESS '- em'
setenv ARCH "`arch`"
setenv MANPATH /usr/share/man:/usr/man:/usr/local/man:/usr/openwin/man:/usr/local/X11R6.3/man:/usr/lang/man:/usr/local/sfw/man:/usr/local/mh/man

#	OS: The operating system, usually either sos4 or sol2
#	ARCH: The general class of hardware architecture, usually sun4 for 
#         Sparc machines
#	KARCH: The kernel architecture, finger distinctions between types of 
#          hardware
#		(CPU instruction sets, perhaps?) - rarely needed
#	PLATFORM: A string indicating the current ARCH-OS combination

setenv OS "`/mix/usr/local/bin/os-type -major`"
setenv ARCH "`arch`"
setenv KARCH "`arch -k`"
setenv PLATFORM "${ARCH}-${OS}"

#         Set these for all shells

set noclobber		# Protects files in SOME cases from accidental death.
umask 077
limit coredumpsize 0    # Turn off core dump files.  DO NOT REMOVE THIS LINE!

#         Aliases for all shells

source ~/.aliases.all

#         Skip remaining setup if not an interactive shell

if ($?USER == 0 || $?prompt == 0) exit

#	Check for mail every 60 seconds; tell me if I have new mail,
#	even if I am in the middle of something or doing nothing.

set mail = (60 /usr/spool/mail/$user)

#          Settings  for interactive shells

set ignoreeof		# Don't log me out if I hit too many control-Ds.
set history = 100	# Remember the last 100 commands, please.
set savehist = 40	# Save the last 40 commands every time I log out.
set notify
set filec

#	Enable file completion:  you can type a few letters of the command
#	or filename, hit the escape key, and the c-shell will complete
#	as much as possible from there.


# file extensions to ignore when using file completion
#set fignore = ( .o .bak .BAK ) 

if ($?prompt) then
  alias sp 'set prompt = `$HOME/.setprompt`" "'
  alias cd "cd \!*; sp; "
  sp
endif

#	Aliases for interactive shells.

source ~/.aliases.interactive
source ~/.aliases.x
stty erase ^H
setenv MPIR_HOME /usr/local/mpich
setenv MPI_ARCH `$MPIR_HOME/bin/tarch`
setenv MPI_LIB $MPIR_HOME/lib/$MPI_ARCH/ch_p4
set path = ($path $MPIR_HOME/bin $MPI_LIB)
setenv MANPATH $MPIR_HOME/man:$MANPATH
